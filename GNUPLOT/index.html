<!doctype html>
<html lang="en-US">
  <head>
    <meta charset="utf-8" />
    <title>Gnuplot examples</title>
    <link href="https://cdn.jsdelivr.net/npm/bootstrap@5.1.3/dist/css/bootstrap.min.css" rel="stylesheet" integrity="sha384-1BmE4kWBq78iYhFldvKuhfTAU6auU8tT94WrHftjDbrCEXSU1oBoqyl2QvZ6jIW3" crossorigin="anonymous">
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/styles/default.min.css">
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/highlight.min.js"></script>

    <!-- and it's easy to individually load additional languages -->
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/languages/go.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/languages/fortran.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.8.0/languages/python.min.js"></script>
    <script>hljs.highlightAll();</script>
  </head>
  <body>
    <div class="container">
      <h1>Gnuplot examples</h1>

      <h2>Profile plots</h2>

      Here averaged streamwise velocity from turbulent channel flow simulation is plotted in logarithmic scale at x.

      Example files: <a href="assets/profile/log_plot.plt">log_plot.plt</a>
      <a href="assets/profile/JAXA1.dat">JAXA1.dat</a>

      <pre>
	<code class="language-python">
	  set terminal pdf size 11cm,9cm
	  set output "Ulog.pdf"
	  #set terminal png size 1100, 900
	  #set output "Ulog.png"

	  set xlabel "y^+" font ",12"
	  set ylabel "U^+" font ",12"
	  set tics font ",10"
	  set xrange [0.3:180]
	  set yrange [0:22]
	  set size square
	  set key top left font ",11"
	  set logscale x
	  set ytics nomirror
	  set bmargin 3

	  k=0.41
	  B=5.6

	  plot \
	  x with lines dashtype 2 lw 1 lc rgb "black" notitle ,\
	  1/k*log(x)+B with lines dashtype 2 lw 1 lc rgb "black" notitle ,\
	  "JAXA1.dat" using ($2):($3) with lines lw 2 lc rgb "red" title "JAXA, CHAN180"
	</code>
      </pre>

      <h4>An example output will be</h4>
      <img src="images/Ulog.png">

      <!-- *************************************************** -->
      <h2>Contour plots</h2>

      This examples shows contour plot of a slice of channel flow, where a sphere is located in the center of the domain.

      Example files: <a href="assets/contour/UVW.dat">UVW.dat</a>
      <a href="assets/contour/moreland.pal">moreland.pal</a>
      <a href="assets/contour/contour.plt">contour.plt</a> 

      <pre>
	<code class="language-python">
	  # Terminal type defines output format
	  # other options include 'svg', 'png', 'ps' etc.
	  set terminal pdf size 18cm, 9cm
	  set output "Contour.pdf"

	  unset key
	  set cblabel "U_x" 

	  set xlabel "X"
	  set ylabel "Y"

	  # Other color styles are available as well
	  #load 'jet.pal'
	  load 'moreland.pal'
	  #load 'spectral.pal'

	  set xrange [0:256]
	  set yrange [0:128]

	  set title "Streamwise velocity contours"

	  # Plot data from the file 'c.dat', 1st and 3rd columns contain X, Y data,
	  # while 5th column contains required values, note, that 5th column is 
	  # scaled and such airthmetics will require braces and dollar sign ($)
	  # before column number.
	  plot 'UVW.dat' using 1:3:($5/2) with image title "A"
	</code>
      </pre>

      <h4>An example output will be</h4>

      <img src="images/Contour.png">

      <!-- *************************************************** -->
      <h2>Vectors</h2>

      This examples shows plotting vector field.

      Example files: <a href="assets/contour/UVW.dat">UVW.dat</a>
      <a href="assets/contour/moreland.pal">moreland.pal</a>
      <a href="assets/contour/contour.plt">vector.plt</a> 

      <pre>
	<code class="language-python">
#!/usr/bin/gnuplot
#
# Plotting a vector field from a data file
#
# AUTHOR: Hagen Wierstorf

reset

# wxt
#set terminal pdf size 350,262 enhanced font 'Verdana,10' persist
#set terminal pdf size 12cm, 10cm font 'Verdana,10'
#set output "1.pdf"
# png
set terminal pngcairo size 2000,1000 enhanced font 'Verdana,16'
set output '1.png'

# functions to calculate the arrow offsets
h = 0.21 # vector size
xf(phi) = h*phi #cos(phi/180.0*pi+pi/2)
yf(phi) = h*phi #sin(phi/180.0*pi+pi/2)


unset key
set cblabel "U_x"

set xlabel "X"
set ylabel "Y"

# Other color styles are available as well
#load 'jet.pal'
load 'moreland.pal'
#load 'spectral.pal'

set xrange [0:256]
set yrange [0:128]

set title "Streamwise velocity contours"

plot \
'UVW.dat' using 1:3:($5/2) with image title "A", \
'UVW.dat' every 16 u ($1-xf($5)):($3-yf($7)):(2*xf($5)):(2*yf($7)):4 \
with vectors ,\
	</code>
      </pre>

      <h4>An example output will be</h4>

      <img src="images/Vector.png" style="width:500px;">

      <!-- *************************************************** -->
      <h2>Vectors</h2>

      This examples shows plotting vector field.

      Example files: <a href="assets/contour/UVW.dat">UVW.dat</a>
      <a href="assets/contour/moreland.pal">moreland.pal</a>
      <a href="assets/contour/contour.plt">vector.plt</a> 

      <pre>
	<code class="language-python">
reset
set xrange [-5:0]
set yrange [2:5]
set isosample 150, 150
set table 'test.dat'
splot sin(1.3*x)*cos(.9*y)+cos(.8*x)*sin(1.9*y)+cos(y*.2*x)
unset table

set cont base
set cntrparam level incremental -3, 0.5, 3
unset surf
set table 'cont.dat'
splot sin(1.3*x)*cos(0.9*y)+cos(.8*x)*sin(1.9*y)+cos(y*.2*x)
unset table

reset
set terminal pdf size 12cm,10cm #font "Arial, 14"
set output "1.pdf"
#set terminal png size 300,300
#set output "2.png"

set xlabel "X"
set ylabel "Y"

set xrange [-5:0]
set yrange [2:5]
unset key
set palette rgbformulae 33,13,10

l '<./cont.sh cont.dat 0 10 0'

#set label 1 "2" at -0.0402301, 3.97315 centre front
#set label 2 "1.5" at -1.47309, 4.15436 centre front
#set label 3 "1" at -0.962898, 4.87919 centre front
#set label 4 "0.5" at -0.488459, 2.0604 centre front
#set label 5 "0" at -4.89899, 4.8856 centre front
#set label 6 "-0.5" at -4.91684, 4.63758 centre front
#set label 7 "-1" at -4.95238, 4.27517 centre front
#set label 8 "-1.5" at -2.87828, 3.67114 centre front
#set label 9 "-2" at -3.28283, 4.61675 centre front
#set label 10 "-2.5" at -3.43434, 3.76853 centre front

p 'test.dat' w ima, '<./cont.sh cont.dat 1 10 0' w l lt -1 lw 1.5
	</code>
      </pre>


      Awk script to produce labels:
      <pre>
	<code class="language-python">
#!/bin/bash
gawk -v d=$2 -v w=$3 -v os=$4 'function abs(x) { return (x>=0?x:-x) }
    {
            if($0~/# Contour/) nr=0
            if(nr==int(os+w/2) && d==0) {i++; a[i]=$1; b[i]=$2; c[i]=$3;}
            if(abs(nr-os-w/2)>w/2 && d==1) print $0
            #if(d==1) print $0
            nr++
    }
    END {   if(d==0) {
                    for(j=1;j<=i;j++)
                    printf "set label %d \"%g\" at %g, %g centre front\n", j, c[j], a[j], b[j]
            }
    }' $1	</code>
      </pre>

      
      <h4>An example output will be</h4>

      <img src="images/Isolines.png" style="width:90%;">

    </div>
    <script src="https://cdn.jsdelivr.net/npm/bootstrap@5.1.3/dist/js/bootstrap.bundle.min.js" integrity="sha384-ka7Sk0Gln4gmtz2MlQnikT1wXgYsOg+OMhuP+IlRH9sENBO0LRn5q+8nbTov4+1p" crossorigin="anonymous"></script>    
  </body>
</html>


